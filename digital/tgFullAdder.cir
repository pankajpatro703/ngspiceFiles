CMOS TG Full Adder
*
*    Copyright 2020 Pankajkumar Patro Licensed under the
*    Educational Community License, Version 2.0 (the "License"); you may
*    not use this file except in compliance with the License. You may
*    obtain a copy of the License at
*    
*    http://www.osedu.org/licenses/ECL-2.0
*
*    Unless required by applicable law or agreed to in writing,
*    software distributed under the License is distributed on an "AS IS"
*    BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express
*    or implied. See the License for the specific language governing
*    permissions and limitations under the License.
*
***************************
* @Author: pankajpatro703 *
*  Released under ECL v2  *
***************************
vdd 1 0 dc    5v
va  a 0 pulse 0v 5v 0ns 10us 10us 1ms 2ms	*input A
vb  b 0 pulse 0v 5v 0ns 10us 10us 2ms 4ms	*input B
vc  c 0 pulse 0v 5v 0ns 10us 10us 4ms 8ms	*input Cin
*output at node S and Co
**********
* n-mos *
mcn1 c1  c   0  0  nmod w=10u l=2u
mcn2 b1  b   0  0  nmod w=10u l=2u
mcn3 nb1 a   b1 0  nmod w=10u l=2u
mcn4 nb  a   b  0  nmod w=10u l=2u
mtn1 s   nb  c  0  nmod w=10u l=2u
mtn2 s   nb1 c1 0  nmod w=10u l=2u
mtn3 co  nb  a  0  nmod w=10u l=2u
mtn4 co  nb1 c  0  nmod w=10u l=2u
* p-mos *
mcp1 c1  c   1  1  pmod w=10u l=2u
mcp2 b1  b   1  1  pmod w=10u l=2u
mcp3 nb1 a   b  1  pmod w=10u l=2u
mcp4 nb  a   b1 1  pmod w=10u l=2u
mtp1 c   nb1 s  1  pmod w=10u l=2u
mtp2 c1  nb  s  1  pmod w=10u l=2u
mtp3 a   nb1 co 1  pmod w=10u l=2u
mtp4 c   nb  co 1  pmod w=10u l=2u
**********
.model nmod nmos vto=1v  kp=20u
.model pmod pmos vto=-1v kp=20u
.tran 0ns 8ms 10us 100us
.control
run
plot v(s) v(co)
plot v(a) v(b) v(c)
.endc
.end
